apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "in.juspay.myapplication"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //https://proandroiddev.com/advanced-android-flavors-part-4-a-new-version-fc2ad80c01bb
    //Resolve library missing fallback.
    //The first string given to the missingDimensionStrategy field is the dimension name.
    // The following comma separated strings are the fallback values to be used in order.
    defaultConfig{
        missingDimensionStrategy 'app', 'app1'
    }

    flavorDimensions "client", "server"
    productFlavors {
        client1 {
            dimension "client"
            applicationIdSuffix ".client1"
        }
        client2 {
            dimension "client"
            applicationIdSuffix ".client2"
        }
        mirror {
            dimension "server"
            matchingFallbacks = ['dev']
        }
        dev {
            dimension "server"
        }
        staging {
            dimension "server"
        }
        production {
            dimension "server"
        }
    }

    applicationVariants.all { variant ->
        def name = variant.getName()
        // Default values
        variant.buildConfigField 'boolean', 'ENABLE_ANALYTICS', 'false'
        // Specific values
        if (name.contains("Dev")) {
            variant.buildConfigField 'String', 'SUBDOMAIN', '"dev"'
        } else if (name.contains("Staging")) {
            variant.buildConfigField 'String', 'SUBDOMAIN', '"staging-api"'
        } else if (name.contains("Production")) {
            variant.buildConfigField 'String', 'SUBDOMAIN', '"api"'
            variant.buildConfigField 'boolean', 'ENABLE_ANALYTICS', 'true'
        }
    }
}



dependencies {
    implementation project(':common')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:2.0.1'
    testImplementation 'junit:junit:4.13.2'
}
